plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.21'
    id 'com.diffplug.spotless' version '6.25.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_21
        freeCompilerArgs.add('-Xjvm-default=all')
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--enable-preview']
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += ['--enable-preview']
}

tasks.withType(Test).configureEach {
    jvmArgs += ['--enable-preview']
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'io.kotest:kotest-runner-junit5:5.8.0'
    testImplementation 'io.kotest:kotest-assertions-core:5.8.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

spotless {
    kotlin {
        ktlint('1.1.1')
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.named('check') {
    dependsOn tasks.named('spotlessCheck')
}
